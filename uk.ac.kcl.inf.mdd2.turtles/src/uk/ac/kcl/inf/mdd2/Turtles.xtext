grammar uk.ac.kcl.inf.mdd2.Turtles with org.eclipse.xtext.common.Terminals

generate turtles "http://www.ac.uk/kcl/inf/mdd2/Turtles"


import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TurtleProgram:
	statements += Statement*
;
	
Statement:
	MoveStatement |
	TurnStatement |
	LoopStatement |
	VariableDeclaration
;

VariableDeclaration:
	"var" name = ID "=" value = INT
;

LoopStatement:
	count = IntExpression "times" "do"
	  statements += Statement+
	"end"
;

MoveStatement:
	command = MoveCommand "(" steps = IntExpression ")"
;

enum MoveCommand:
	forward | backward
;

TurnStatement:
	"turn" command = TurnCommand "by" degrees = RealLiteral "degrees"
;

enum TurnCommand:
	left | right
;

Addition returns Expression:
	Multiplication ({Addition.left = current} '+' right += Multiplication)*
;

Multiplication returns Expression:
	IntExpression ({Multiplication.left = current} '*' right += IntExpression)*
;

IntExpression:
	val = Primary
;

Primary returns Expression:
	IntVarExpression |
	IntLiteral |
	RealLiteral |
	"(" Addition ")"
;

IntLiteral:
	val = INT
;

RealLiteral:
	val = REAL
;

IntVarExpression:
	var = [VariableDeclaration]
;

REAL returns ecore::EFloat hidden():
	INT? "." INT
; 

